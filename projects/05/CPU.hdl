// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // A Mux 
    Mux16(a=instruction, b=outALU, sel=instruction[15], out=inA);

    // A Register
    Not(in=instruction[15], out=notIns15);
    Or(a=instruction[5], b=notIns15, out=loadA);
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);

    // A-M Mux
    Mux16(a=outA, b=inM, sel=instruction[12], out=inALU2);

    // D Register
    And(a=instruction[4], b=instruction[15], out=loadD);
    DRegister(in=outALU, load=loadD, out=inALU1);

    // ALU
    ALU(x=inALU1, y=inALU2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=outALU, zr=zrALU, ng=ngALU);
  
    // M Register
    And(a=instruction[3], b=instruction[15], out=writeM);

    // PC
    Not(in=zrALU, out=notZrALU);
    Not(in=ngALU, out=notNgALU);

    And(a=notZrALU, b=notNgALU, out=case1);
    Or(a=zrALU, b=ngALU, out=case6);

    Mux(a=false, b=case1, sel=instruction[0], out=case01);
    Mux(a=zrALU, b=notNgALU, sel=instruction[0], out=case23);
    Mux(a=ngALU, b=notZrALU, sel=instruction[0], out=case45);
    Mux(a=case6, b=true, sel=instruction[0], out=case67);

    Mux(a=case01, b=case23, sel=instruction[1], out=case03);
    Mux(a=case45, b=case67, sel=instruction[1], out=case47);

    Mux(a=case03, b=case47, sel=instruction[2], out=case07);
    And(a=case07, b=instruction[15], out=loadPC);

    PC(in=outA, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
}
